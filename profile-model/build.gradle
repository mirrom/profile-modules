plugins {
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java"
    id "org.springframework.boot" version "2.4.3"
}

group "com.example.profile"
version "1.0-SNAPSHOT"

def hibernateJpaVersion = "1.0.2.Final"
def javaxAnnotationVersion = "1.3.2"
def javaxValidationVersion = "2.0.1.Final"
def junitJupiterVersion = "5.7.1"
def lombokVersion = "1.18.18"
def lombokMapstructBindingVersion = "0.2.0"
def mapstructVersion = "1.4.2.Final"
def querydslVersion = "4.4.0"
def springfoxVersion = "3.0.0"

bootJar {
    enabled = false
}

jar {
    enabled = true
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.data:spring-data-mongodb"
    
    implementation "com.querydsl:querydsl-apt:${querydslVersion}"
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    implementation "io.springfox:springfox-boot-starter:${springfoxVersion}"
    implementation "io.springfox:springfox-bean-validators:${springfoxVersion}"
    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"
    implementation "javax.validation:validation-api:${javaxValidationVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jpa"
    annotationProcessor "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"
    annotationProcessor "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:${hibernateJpaVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

test {
    useJUnitPlatform()
}
